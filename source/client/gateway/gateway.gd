extends Control


const CredentialsUtils: GDScript = preload("res://source/common/utils/credentials_utils.gd")

var account_id: int
var account_name: String
var session_id: String
var local_id: String

var current_world_id: int
var current_character_id: int
var selected_skin_id: int = 1

var menu_stack: Array[Control]

@onready var main_panel: PanelContainer = $MainPanel
@onready var login_panel: PanelContainer = $LoginPanel
@onready var popup_panel: PanelContainer = $PopupPanel

@onready var back_button: Button = $BackButton

@onready var http_request: HTTPRequest = $HTTPRequest


func _ready() -> void:
	menu_stack.append(main_panel)
	back_button.hide()

	prepare_character_creation_menu()

	local_id = CmdlineUtils.get_parsed_args().get("id", "")

	if not await try_auto_login():
		popup_panel.hide()
		$MainPanel.show()
		$MainPanel/VBoxContainer/VBoxContainer/LoginButton.grab_focus()


func handle_success_login(d: Dictionary) -> void:
	var worlds: Dictionary = d.get("w", {})

	session_id = d.get("session_id", 0)

	account_name = d.get("name", "")
	account_id = d.get("id", 0)
	current_character_id = d.get("character_id", 0)

	var last_world_name: String = d.get("world_name", "")
	var is_last_world_online: bool = false

	for world_id: String in worlds:
		if worlds[world_id].get("info", {}).get("name", "-1") == last_world_name:
			current_world_id = world_id.to_int()
			is_last_world_online = true

	populate_worlds(worlds)

	if is_last_world_online:
		$AlreadyConnectedPanel/ContinueButton.text = tr("CONTINUE_WORLD_ACC") % [last_world_name, account_name]
		popup_panel.hide()
		_show($AlreadyConnectedPanel, false)
	else:
		popup_panel.hide()
		$MainPanel.show()
		fill_connection_info(account_name, account_id)
		_show($WorldSelection, false)


func do_request(
	method: HTTPClient.Method,
	path: String,
	payload: Dictionary,
) -> Dictionary:
	if http_request.get_http_client_status() == HTTPClient.Status.STATUS_CONNECTED:
		return {"error": "request_error"}

	var custom_headers: PackedStringArray
	custom_headers.append("Content-Type: application/json")
	
	var error: Error = http_request.request(
		path,
		custom_headers,
		method,
		JSON.stringify(payload)
	)

	if error != OK:
		push_error("An error occurred in the HTTP request.")
		return {ok=false, error="request_error", code=error}

	var args: Array = await http_request.request_completed
	var result: int = args[0]
	if result != OK:
		return {"error": 1, "msg": "TIMEOUT?", "code": result}

	var response_code: int = args[1]
	var headers: PackedStringArray = args[2]
	var body: PackedByteArray = args[3]

	var data: Variant = JSON.parse_string(body.get_string_from_ascii())
	if data is Dictionary:
		return data
	return {"error": 1, "msg": "Wrong data format received."}


func _show(next: Control, can_back: bool = true) -> void:
	if menu_stack.size():
		menu_stack.back().hide()
	if not can_back:
		menu_stack.clear()
	next.show()
	menu_stack.append(next)
	back_button.visible = can_back


func _on_login_button_pressed() -> void:
	_show(login_panel)


func _on_login_login_button_pressed() -> void:
	var account_name_edit: LineEdit = $LoginPanel/VBoxContainer/VBoxContainer/VBoxContainer/LineEdit
	var password_edit: LineEdit = $LoginPanel/VBoxContainer/VBoxContainer/VBoxContainer2/LineEdit

	var username: String = account_name_edit.text
	var password: String = password_edit.text

	var login_button: Button = $LoginPanel/VBoxContainer/VBoxContainer/LoginButton
	login_button.disabled = true
	if (
		CredentialsUtils.validate_username(username).code != CredentialsUtils.UsernameError.OK
		or CredentialsUtils.validate_password(password).code != CredentialsUtils.UsernameError.OK
	):
		#await popup_panel.confirm_message(str(response))
		login_button.disabled = false
		return

	popup_panel.display_waiting_popup()
	var response: Dictionary = await request_login(username, password)
	if response.has("error"):
		await popup_panel.confirm_message(str(response))
		login_button.disabled = false
		return

	session_id = response.get("session_id")

	save_refresh_token("%s\n%s" % [username, password], "user://%ssession.dat" % local_id)

	
	populate_worlds(response.get("w", {}))
	fill_connection_info(response["name"], response["id"])

	popup_panel.hide()
	_show($WorldSelection, false)


func _on_guest_button_pressed() -> void:
	popup_panel.display_waiting_popup()

	var d: Dictionary = await do_request(
		HTTPClient.Method.METHOD_POST,
		GatewayAPI.guest(),
		{}
	)
	if d.has("error"):
		await popup_panel.confirm_message(str(d))
		return

	session_id = d.get("session_id", 0)

	fill_connection_info(d.get("name", ""), d.get("id", 0))
	populate_worlds(d.get("w", {}))

	popup_panel.hide()
	_show($WorldSelection, false)


func _on_world_selected(world_id: int) -> void:
	$WorldSelection.hide()
	popup_panel.display_waiting_popup()
	var d: Dictionary = await do_request(
		HTTPClient.Method.METHOD_POST,
		GatewayAPI.world_characters(),
		{
			GatewayAPI.KEY_WORLD_ID: world_id,
			GatewayAPI.KEY_ACCOUNT_ID: account_id,
			GatewayAPI.KEY_ACCOUNT_USERNAME: account_name,
			GatewayAPI.KEY_TOKEN_ID: session_id
		}
	)
	if d.has("error"):
		await popup_panel.confirm_message(str(d))
		$WorldSelection.show()
		return

	var container: HBoxContainer = $CharacterSelection/VBoxContainer/HBoxContainer
	var i: int = 0
	var character_id: String
	for button: Button in container.get_children():
		if button.pressed.is_connected(_on_character_selected):
			button.pressed.disconnect(_on_character_selected)
		if d.size() > i:
			character_id = d.keys()[i]
			if not d.get(d.keys()[i], {}).has_all(["name","class", "level"]):
				continue
			button.text = tr("NAME_CLASS_LEVEL") % [
				d[character_id]["name"],
				d[character_id]["class"],
				d[character_id]["level"],
			]
			button.pressed.connect(_on_character_selected.bind(world_id, character_id.to_int()))
		else:
			button.text = tr("CREATE_NEW_CHAR")
			button.pressed.connect(_on_character_selected.bind(world_id, -1))
		i += 1
	popup_panel.hide()
	_show($CharacterSelection)


func _on_character_selected(world_id: int, character_id: int) -> void:
	current_world_id = world_id
	if character_id == -1:
		_show($CharacterCreation)
		return

	$CharacterSelection.hide()
	$BackButton.hide()
	popup_panel.display_waiting_popup()

	var d: Dictionary = await do_request(
		HTTPClient.Method.METHOD_POST,
		GatewayAPI.world_enter(),
		{
			GatewayAPI.KEY_TOKEN_ID: session_id,
			GatewayAPI.KEY_ACCOUNT_USERNAME: account_name,
			GatewayAPI.KEY_WORLD_ID: world_id,
			GatewayAPI.KEY_CHAR_ID: character_id
		}
	)
	if d.has("error"):
		await popup_panel.confirm_message(str(d))
		$CharacterSelection.show()
		$BackButton.show()
		return

	Client.connect_to_server(d["address"], d["port"], d["auth-token"])
	queue_free.call_deferred()


func _on_create_character_button_pressed() -> void:
	var username_edit: LineEdit = $CharacterCreation/VBoxContainer/VBoxContainer/HBoxContainer2/LineEdit

	var create_button: Button = $CharacterCreation/VBoxContainer/VBoxContainer/CreateButton
	create_button.disabled = true
	$BackButton.hide()
	$CharacterCreation.hide()

	var result: Dictionary
	result = CredentialsUtils.validate_username(username_edit.text)
	if result.code != CredentialsUtils.UsernameError.OK:
		await popup_panel.confirm_message(tr("USERNAME") + result.message)
		create_button.disabled = false
		$BackButton.show()
		$CharacterCreation.show()
		return

	popup_panel.display_waiting_popup()
	var d: Dictionary = await do_request(
		HTTPClient.Method.METHOD_POST,
		GatewayAPI.world_create_char(),
		{
			GatewayAPI.KEY_TOKEN_ID: session_id,
			"data": {
				"name": username_edit.text,
				"skin": selected_skin_id,
			},
			GatewayAPI.KEY_ACCOUNT_USERNAME: account_name,
			GatewayAPI.KEY_WORLD_ID: current_world_id
		}
	)
	if d.has("error"):
		await popup_panel.confirm_message(str(d))
		create_button.disabled = false
		$CharacterCreation.show()
		return

	Client.connect_to_server(
		d["address"],
		d["port"],
		d["auth-token"]
	)
	queue_free.call_deferred()


func create_account() -> void:
	var name_edit: LineEdit = $CreateAccountPanel/VBoxContainer/VBoxContainer/VBoxContainer/LineEdit
	var password_edit: LineEdit = $CreateAccountPanel/VBoxContainer/VBoxContainer/VBoxContainer2/LineEdit
	var password_repeat_edit: LineEdit = $CreateAccountPanel/VBoxContainer/VBoxContainer/VBoxContainer3/LineEdit

	if password_edit.text != password_repeat_edit.text:
		await popup_panel.confirm_message(tr("PASSWORDS_DONT_MATCH"))
		return
	
	var result: Dictionary
	result = CredentialsUtils.validate_username(name_edit.text)
	if result.code != CredentialsUtils.UsernameError.OK:
		await popup_panel.confirm_message(tr("USERNAME") + result.message)
		return
	result = CredentialsUtils.validate_password(password_edit.text)
	if result.code != CredentialsUtils.UsernameError.OK:
		await popup_panel.confirm_message(tr("PASSWORD") + ":\n" + result.message)
		return
	
	$CreateAccountPanel.hide()
	popup_panel.display_waiting_popup()

	var d: Dictionary = await do_request(
		HTTPClient.Method.METHOD_POST,
		GatewayAPI.account_create(),
		{
			GatewayAPI.KEY_ACCOUNT_USERNAME: name_edit.text,
			GatewayAPI.KEY_ACCOUNT_PASSWORD: password_edit.text,
		}
	)
	if d.has("error"):
		await popup_panel.confirm_message(str(d))
		$CreateAccountPanel.show()
		return
	
	save_refresh_token(name_edit.text + "\n" + password_edit.text, "user://%ssession.dat" % local_id)


	fill_connection_info(d["name"], d["id"])
	populate_worlds(d.get("w", {}))
	
	popup_panel.hide()
	_show($WorldSelection, false)


func _on_create_account_button_pressed() -> void:
	_show($CreateAccountPanel)


func populate_worlds(world_info: Dictionary) -> void:
	var container: HBoxContainer = $WorldSelection/VBoxContainer/HBoxContainer
	for child: Node in container.get_children():
		child.queue_free()
	for world_id: String in world_info:
		add_world_card(world_info.get(world_id, {}).get("info", {}), world_id.to_int())
	
	if world_info.is_empty():
		popup_panel.show_reconnect_popup()


func fill_connection_info(_account_name: String, _account_id: int) -> void:
	account_name = _account_name
	account_id = _account_id
	$ConnectionInfo.text = tr("ACC_NAME_ACC_ID") % [
		account_name, account_id
	]


func add_world_card(world_info: Dictionary, world_id: int) -> Button:
	var container: HBoxContainer = $WorldSelection/VBoxContainer/HBoxContainer

	var button: Button = Button.new()
	button.custom_minimum_size = Vector2(150.0, 250.0)
	button.pressed.connect(_on_world_selected.bind(world_id))

	var text_label: RichTextLabel = RichTextLabel.new()
	text_label.bbcode_enabled = true
	text_label.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	text_label.vertical_alignment = VERTICAL_ALIGNMENT_CENTER
	text_label.set_anchors_preset(Control.PRESET_FULL_RECT)
	text_label.mouse_filter = Control.MOUSE_FILTER_PASS

	text_label.append_text(
		"[font_size=20][b]%s[/b][/font_size]\n" % world_info.get("name", "Unknown World")
	)
	text_label.append_text(
		"\n[i][font_size=12]\"%s\"[/font_size][/i]\n" % tr(world_info.get("motd", ""))
	)
	text_label.append_text(
		"\n[font_size=13][b]%s[/b][/font_size]\n" % "PvP" if world_info.get("pvp", true) else "No PvP"
	)

	button.add_child(text_label)

	container.add_child(button)
	return button


func _on_continue_button_pressed() -> void:
	$AlreadyConnectedPanel.hide()
	popup_panel.display_waiting_popup()
	var d: Dictionary = await do_request(
		HTTPClient.Method.METHOD_POST,
		GatewayAPI.world_enter(),
		{
			GatewayAPI.KEY_TOKEN_ID: session_id,
			GatewayAPI.KEY_ACCOUNT_USERNAME: account_name,
			GatewayAPI.KEY_WORLD_ID: current_world_id,
			GatewayAPI.KEY_CHAR_ID: current_character_id
		}
	)
	if d.has("error"):
		await popup_panel.confirm_message(str(d))
		$AlreadyConnectedPanel.show()
		return

	Client.connect_to_server(d["address"], d["port"], d["auth-token"])
	queue_free.call_deferred()


func _on_change_button_pressed() -> void:
	_show($WorldSelection, false)


func _on_settings_button_pressed() -> void:
	$Settings.visible = not $Settings.visible


#func _on_swap_theme_button_toggled(toggled_on: bool) -> void:
	#if not $AudioStreamPlayer.playing:
	#	$AudioStreamPlayer.play()
	#if toggled_on:
	#	$Background.texture = preload("uid://cfihbj71a4y35")
	#	Client.theme = preload("uid://c2nr0o8v7vb75")
	#else:
	#	$Background.texture = preload("uid://cn5blfyqokda6")
	#	Client.theme = preload("uid://cf1ayo3dckj67")
	#theme = Client.theme


func _on_back_button_pressed() -> void:
	if menu_stack.size():
		menu_stack.pop_back().hide()
		if menu_stack.size():
			menu_stack.back().show()
		if menu_stack.size() < 2:
			back_button.hide()


# Helpers
func request_login(username: String, password: String) -> Dictionary:
	return await do_request(
		HTTPClient.Method.METHOD_POST,
		GatewayAPI.login(),
		{
			GatewayAPI.KEY_ACCOUNT_USERNAME: username,
			GatewayAPI.KEY_ACCOUNT_PASSWORD: password,
		}
	)

func request_enter_world() -> Dictionary:
	return await do_request(
			HTTPClient.Method.METHOD_POST,
			GatewayAPI.world_enter(),
			{
				GatewayAPI.KEY_TOKEN_ID: session_id,
				GatewayAPI.KEY_ACCOUNT_USERNAME: account_name,
				GatewayAPI.KEY_WORLD_ID: current_world_id,
				GatewayAPI.KEY_CHAR_ID: current_character_id
			}
		)


func prepare_character_creation_menu() -> void:
	var animated_sprite_2d: AnimatedSprite2D = $CharacterCreation/VBoxContainer/VBoxContainer/HBoxContainer/VBoxContainer2/CenterContainer/Control/AnimatedSprite2D
	animated_sprite_2d.play(&"run")
	var v_box_container: GridContainer = $CharacterCreation/VBoxContainer/VBoxContainer/HBoxContainer/VBoxContainer/VBoxContainer
	for button: Button in v_box_container.get_children():
		button.pressed.connect(
			func() -> void:
				var sprite: SpriteFrames = ContentRegistryHub.load_by_slug(&"sprites", button.text.to_lower()) as SpriteFrames
				if not sprite:
					return
				selected_skin_id = ContentRegistryHub.id_from_slug(&"sprites", button.text.to_lower())
				if selected_skin_id == 0:
					selected_skin_id = 1
				animated_sprite_2d.sprite_frames = sprite
				animated_sprite_2d.play(&"run")
		)


# Ideally we must not save credentials locally even if crypted,
# saving a temporary token given by the server is the way. 
func try_auto_login() -> bool:
	var refresh_token: String
	var file_path: String = "user://session.dat"

	$MainPanel.hide()
	popup_panel.display_waiting_popup()

	# Await timer to let the gateway server boot up. 
	await get_tree().create_timer(2.0).timeout

	file_path  = "user://%ssession.dat" % local_id
	refresh_token = load_refresh_token(file_path)
	if refresh_token.is_empty():
		return false

	# Old way
	#var debug_credentials: Dictionary = ConfigFileUtils.load_section_safe(debug_id, "res://data/config/client_config.cfg", ["username", "password"])
	#var response: Dictionary = await request_login(debug_credentials["username"], debug_credentials["password"]) 

	var response: Dictionary = await request_login(
		refresh_token.get_slice("\n", 0), #Username
		refresh_token.get_slice("\n", 1) #Password
	)
	if response.has("error"):
		await popup_panel.confirm_message(str(response))
		return false
	else:
		handle_success_login(response)
	return true


# Can be changed / randomized each build
const LOCAL_PASS: String = "LOCAL_PASSWORD"
func save_refresh_token(token: String, file_path: String) -> void:
	var file: FileAccess = FileAccess.open_encrypted_with_pass(file_path, FileAccess.WRITE, LOCAL_PASS)
	if file:
		file.store_string(token)
		file.close()
	else:
		printerr(error_string(FileAccess.get_open_error()))


func load_refresh_token(file_path: String) -> String:
	if not FileAccess.file_exists(file_path):
		return ""
	var file: FileAccess = FileAccess.open_encrypted_with_pass(file_path, FileAccess.READ, LOCAL_PASS)
	if not file:
		printerr(error_string(FileAccess.get_open_error()))
		return ""
	var token: String = file.get_as_text()
	file.close()
	return token
